/**
 * File:	modules/Kerberos.ycp
 * Module:	Configuration of Kerberos client
 * Summary:	client configuration data, I/O functions.
 * Authors: Jiri Suchomel <jsuchome@suse.cz>
 */

{

module "Kerberos";
textdomain "kerberos";

import "Summary";

// if pam_krb5 module is used for login
global boolean use_pam_krb = false;

// required/optional/sufficient
global string control = "";
string saved_control = "";

// argumets to pam module
global string arguments = "";

// default realm name
global string default_realm = "";

// adress of KDC (key distribution centre) server for default realm
global string kdc = "";

//global string clockskew = "";

global boolean pam_modified = false;


/**
 * Get all the Kerberos configuration from a map.
 * @return	success
 */
global define boolean Import (map settings) ``{
	return true;
}

/**
 * Only set variables, without checking anything
 * @return: void
 */
global define void Set(map settings)  ``{

	return;
}

/**
 * Dump the Kerberos settings to a map, for autoinstallation use.
 */
global define map Export () ``{
	return $[];
}

/**
 * Reads Kerberos settings from the SCR
 * @return success
 */
global define boolean Read () ``{

    // first, read the pam settings
    // currently, only /etc/pam.d/login is checked
    list pam_login = SCR::Read(.pam.login);
    foreach (`line, pam_login, ``{
        if ( line["type"]:"" == "auth" &&
             issubstring (line["module"]:"", "pam_krb5.so"))
        {
            use_pam_krb = true;
            // only the first is taken
            if (control == "")
                control = line["control"]:"";
            if (arguments == "")
                arguments = line["arguments"]:"";
        }
    });

    saved_control = control;

    // now read the settings from /etc/krb5.conf
    if (SCR::Execute (.target.bash, "/usr/bin/test -e /etc/krb5.conf") == 0)
    {
        y2debug("krb5.conf sections: %1", SCR::Dir(.etc.krb5_conf.s));

        default_realm = ReadString (.etc.krb5_conf.v.libdefaults.default_realm);

        clockskew = ReadString (.etc.krb5_conf.v.libdefaults.clockskew);

        kdc = ReadString ( add ( add (.etc.krb5_conf.v, default_realm), "kdc"));
    }
    else
        SCR::Execute (.target.bash, sformat("/usr/bin/touch /etc/krb5.conf"));

    return true;
}

/**
 * Saves Kerberos configuration.
 * (No parameters because it is too short to abort)
 * @return true on success
 */
global define boolean Write (boolean writeonly) ``{

    boolean pam_installed = false;
    boolean ret = true;
    list pam_files = ["login", "xlock"]; // FIXME: xdm doesn't work now !!

    // -- pam settings
    if (pam_modified)
    {
        if (use_pam_krb)
        {
            if (SCR::Execute(.target.bash, "rpm -q pam_krb5") ==0)
               pam_installed = true;
            else
            {
                // Popup text (required application):
                if (UI::YesNoPopup(_("To configure PAM autentification with Kerberos,
the package \"pam_krb5\" is necessary.
Install this package now?")))
                {
                    include "require.ycp"; // is it possible to have it here??
                    pam_installed = DoInstallAndRemove(["pam_krb5"], []);
                }
            }
            if (pam_installed)
            {
                // -- write to /etc/pam.d/*
                UpdatePamFiles (pam_files);
            }
            else
                // instalation not succesful ?
                ret = false;
        }
        else
        {
            UpdatePamFiles (pam_files);
        }
    }

    // -- write to /etc/krb5.conf

    // change the default realm name
    SCR::Write(.etc.krb5_conf.v.libdefaults.default_realm, default_realm);

    if (contains (SCR::Dir(.etc.krb5_conf.s), default_realm))
    {
        // update the default realm settings
        SCR::Write(
            add (add (.etc.krb5_conf.v, default_realm), "kdc"), kdc);
    }
    else
    {
        // specify the type of this subsection
        SCR::Write( add (.etc.krb5_conf.st.realms, default_realm), 1);
        // write the settings of the new default realm
        SCR::Write(
            add (add (.etc.krb5_conf.v.realms, default_realm), "kdc"), kdc);
    }

    return ret;
}

global define string ReadString (path path_to_value) ``{

    string value = SCR::Read (path_to_value);
    if (value == nil)
        value = "";

    return value;
}

global define string ReadFile (string name) ``{

    string ret = SCR::Read (.target.string, "/etc/pam.d/" + name);

    if (findlastof (ret, "\n") < size (ret) - 1)
        ret = ret + "\n";
    y2milestone ("file /etc/pam.d/%1: %2", name, ret);
    return ret;

}

global define string RemoveOldEntry (string file, string old) ``{

    y2milestone ("file : %1", file);
    list file_list = splitstring (file, "\n");
    file_list = filter (`entry, file_list, ``{

//        list entry_list = splitstring (entry, " "); // what about \t ?
//    y2milestone ("entry_list : %1", entry_list);
/*
        if (contains (entry_list, "auth") &&
            contains (entry_list, "pam_krb5.so") &&
            contains (entry_list, old))
            return false;
*/
        if (findfirstof (entry, "#") != 0 &&
            issubstring (entry, "auth") &&
            issubstring (entry, "pam_krb5.so") &&
            issubstring (entry, old))
            return false;
        else return true;
    });

    file = mergestring (file_list, "\n");
    y2milestone ("file : %1", file);
    return file;
}

global define boolean UpdatePamFiles (list files) ``{

    foreach (`filename, files, ``{

        string content = ReadFile(filename);
        string file_path = "/etc/pam.d/" + filename;
        path unix2_path = add( add( add (.pam, filename), "auth"), "pam_unix2");

        if ((saved_control != "" && saved_control != control) ||
             !use_pam_krb)
        {
            // remove the previously used entry
            content = RemoveOldEntry (content, saved_control);
        }

        if (use_pam_krb)
        {
            // add the new entry is:
            string entry = "auth " + control + " pam_krb5.so missing_keytab_ok";
            if (control == "sufficient")
            {
                //sufficient - 1st line
                content = entry + "\n" + content;
                SCR::Write (.target.string, file_path, content);
                // change params for pam_unix
                SCR::Write (unix2_path, "+use_first_pass");
            }
            else if (control == "optional")
            {
                entry = entry + " try_first_pass";
                //optional - last line
                content = content + entry;
                SCR::Write (.target.string, file_path, content);
                // change params for pam_unix
                SCR::Write (unix2_path, "-use_first_pass");
            }
        }
        else
        {
            // remove pam_krb5 from /etc/pam.d
            SCR::Write (.target.string, file_path, content);
        }
    });
}


/**
 * Summary()
 * returns html formated configuration summary
 * @return string
 */
global define Summary () ``{

	string summary = "";
	return summary;
}

}
